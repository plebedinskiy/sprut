
&НаКлиенте
Процедура СформироватьОтчет(Команда)
	СформироватьОтчетНаСервере();
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетНаСервере()

	ТабДок.Очистить();
		
	РФвЗОбъект = РеквизитФормыВЗначение("Отчет");
	Макет 	 = РФвЗОбъект.ПолучитьМакет("МакетСопоставленияДокументов");
		
	ОбластьШапка             			= Макет.ПолучитьОбласть("ОбластьШапкаРеестраДокументов");
	ОбластьСтрокаРеестраДокументов 		= Макет.ПолучитьОбласть("ОбластьСтрокаРеестраДокументов");

	
	Запрос = Новый Запрос(ВернутьТекстЗапросаСопоставленияПлатежейНаСервере());
	Запрос.УстановитьПараметр("ДатаНачала", 		Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",		КонецДня(Период.ДатаОкончания));
	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	ТабДок.Вывести(ОбластьШапка);
	Номер = 1;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	
		ОбластьСтрокаРеестраДокументов.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
		ОбластьСтрокаРеестраДокументов.Параметры.Номер = Номер;
		ТабДок.Вывести(ОбластьСтрокаРеестраДокументов, ВыборкаДетальныеЗаписи.Уровень());
	
		Номер = Номер + 1;
		
	КонецЦикла;
	

КонецПроцедуры



Функция ВернутьТекстЗапросаСопоставленияПлатежейНаСервере()

	Возврат "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Контрагенты.Ссылка КАК Контрагент
		|ПОМЕСТИТЬ ВТ_КонтрагентыДляМониторинга
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	(Контрагенты.Ссылка В
		|				(ВЫБРАТЬ
		|					КонтрагентыДляМониторинга.Контрагент
		|				ИЗ
		|					РегистрСведений.sprt_КонтрагентыДляМониторинга КАК КонтрагентыДляМониторинга)
		|			ИЛИ Контрагенты.ГоловнойКонтрагент В
		|				(ВЫБРАТЬ
		|					КонтрагентыДляМониторинга.Контрагент
		|				ИЗ
		|					РегистрСведений.sprt_КонтрагентыДляМониторинга КАК КонтрагентыДляМониторинга))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеБезналичныхДенежныхСредств.Ссылка КАК ДокументИзБазы,
		|	sprt_ПлатежноеПоручение.Ссылка КАК ДокументИзОтчета,
		|	sprt_ПлатежноеПоручение.Сумма КАК Сумма,
		|	ПоступлениеБезналичныхДенежныхСредств.СуммаДокумента КАК СуммаДокумента
		|ПОМЕСТИТЬ ВТ_СопоставленныйБанк
		|ИЗ
		|	Документ.sprt_ПлатежноеПоручение КАК sprt_ПлатежноеПоручение
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеБезналичныхДенежныхСредств КАК ПоступлениеБезналичныхДенежныхСредств
		|		ПО sprt_ПлатежноеПоручение.НомерПлатежногоПоручения = ПоступлениеБезналичныхДенежныхСредств.НомерВходящегоДокумента
		|			И sprt_ПлатежноеПоручение.ДатаПлатежногоПоручения = ПоступлениеБезналичныхДенежныхСредств.ДатаВходящегоДокумента
		|			И (ПоступлениеБезналичныхДенежныхСредств.Проведен)
		|			И (ПоступлениеБезналичныхДенежныхСредств.ПроведеноБанком)
		|ГДЕ sprt_ПлатежноеПоручение.ДатаПлатежногоПоручения МЕЖДУ &ДатаНачала И &ДатаОкончания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеБезналичныхДенежныхСредств.Ссылка КАК ДокументИзБазы,
		|	ПоступлениеБезналичныхДенежныхСредств.СуммаДокумента КАК СуммаДокумента
		|ПОМЕСТИТЬ ВТ_НесопоставленныйБанкИз1с
		|ИЗ
		|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК ПоступлениеБезналичныхДенежныхСредств
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СопоставленныйБанк КАК ВТ_СопоставленныйБанк
		|		ПО ПоступлениеБезналичныхДенежныхСредств.Ссылка = ВТ_СопоставленныйБанк.ДокументИзБазы
		|ГДЕ
		|	ПоступлениеБезналичныхДенежныхСредств.ДатаВходящегоДокумента МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ПоступлениеБезналичныхДенежныхСредств.Проведен
		|	И ПоступлениеБезналичныхДенежныхСредств.ПроведеноБанком
		|	И ПоступлениеБезналичныхДенежныхСредств.Контрагент В
		|			(ВЫБРАТЬ
		|				ВТ_КонтрагентыДляМониторинга.Контрагент
		|			ИЗ
		|				ВТ_КонтрагентыДляМониторинга КАК ВТ_КонтрагентыДляМониторинга)
		|	И ВТ_СопоставленныйБанк.ДокументИзБазы ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НесопоставленныйБанкИз1с.ДокументИзБазы КАК ДокументИзБазы,
		|	NULL КАК ДокументИзОтчета,
		|	NULL КАК ДокументИзОтчетаНаименование,
		|	NULL КАК Сумма,
		|	ВТ_НесопоставленныйБанкИз1с.СуммаДокумента КАК СуммаДокумента,
		|	ВТ_НесопоставленныйБанкИз1с.ДокументИзБазы.Организация КАК Организация,
		|	ВТ_НесопоставленныйБанкИз1с.ДокументИзБазы.Контрагент КАК Контрагент
		|ИЗ
		|	ВТ_НесопоставленныйБанкИз1с КАК ВТ_НесопоставленныйБанкИз1с
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_СопоставленныйБанк.ДокументИзБазы,
		|	ВТ_СопоставленныйБанк.ДокументИзОтчета,
		|	ВТ_СопоставленныйБанк.ДокументИзОтчета.Наименование,
		|	ВТ_СопоставленныйБанк.Сумма,
		|	ВТ_СопоставленныйБанк.СуммаДокумента,
		|	ЕСТЬNULL(ВТ_СопоставленныйБанк.ДокументИзБазы.Организация, ВТ_СопоставленныйБанк.ДокументИзОтчета.ОрганизацияИзОтчета),
		|	ЕСТЬNULL(ВТ_СопоставленныйБанк.ДокументИзБазы.Контрагент, ВТ_СопоставленныйБанк.ДокументИзОтчета.ПартнерИзОтчета)
		|ИЗ
		|	ВТ_СопоставленныйБанк КАК ВТ_СопоставленныйБанк
		|
		|
		|
		|
		|";
	

КонецФункции