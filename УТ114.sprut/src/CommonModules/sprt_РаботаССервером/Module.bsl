Процедура ЗаполнитьКлючевыеДанныеИСопоставления(ТЗРеестрОплат) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТЗВзаиморасчетов.Организация КАК Организация,
	|	ТЗВзаиморасчетов.Контрагент КАК Контрагент,
	|	ТЗВзаиморасчетов.ДатаНачала КАК ДатаНачала,
	|	ТЗВзаиморасчетов.ДатаОкончания КАК ДатаОкончания,
	|	ТЗВзаиморасчетов.Документ КАК Документ,
	|	ТЗВзаиморасчетов.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	ТЗВзаиморасчетов.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток,
	|	ТЗВзаиморасчетов.СуммаРасход КАК СуммаРасход,
	|	ТЗВзаиморасчетов.СуммаПриход КАК СуммаПриход,
	|	ТЗВзаиморасчетов.ИмяФайла КАК ИмяФайла
	|ПОМЕСТИТЬ ВТТЗВзаиморасчетов
	|ИЗ
	|	&ТЗВзаиморасчетов КАК ТЗВзаиморасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТТЗВзаиморасчетов.Организация
	|ПОМЕСТИТЬ ВТ_РазличныеОрганизации
	|ИЗ
	|	ВТТЗВзаиморасчетов КАК ВТТЗВзаиморасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТТЗВзаиморасчетов.Контрагент
	|ПОМЕСТИТЬ ВТ_РазличныеКонтрагенты
	|ИЗ
	|	ВТТЗВзаиморасчетов КАК ВТТЗВзаиморасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РазличныеОрганизации.Организация,
	|	Организации.Ссылка
	|ИЗ
	|	ВТ_РазличныеОрганизации КАК ВТ_РазличныеОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.sprt_НашиОрганизацииИзОтчетов КАК Организации
	|		ПО ВТ_РазличныеОрганизации.Организация = Организации.Наименование
	|ГДЕ
	|	Организации.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РазличныеКонтрагенты.Контрагент,
	|	Партнеры.Ссылка
	|ИЗ
	|	ВТ_РазличныеКонтрагенты КАК ВТ_РазличныеКонтрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.sprt_ПартнерыИзОтчетов КАК Партнеры
	|		ПО ВТ_РазличныеКонтрагенты.Контрагент = Партнеры.Наименование
	|ГДЕ
	|	Партнеры.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ТЗВзаиморасчетов", ТЗРеестрОплат);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат.Получить(Результат.ВГраница() - 1).Выбрать();
	Пока Выборка.Следующий() Цикл
		НовЭлемент = Справочники.sprt_НашиОрганизацииИзОтчетов.СоздатьЭлемент();
		НовЭлемент.Наименование = Выборка.Организация;
		НовЭлемент.Записать();
	КонецЦикла;
		
	Выборка = Результат.Получить(Результат.ВГраница()).Выбрать();
	Пока Выборка.Следующий() Цикл
		НовЭлемент = Справочники.sprt_ПартнерыИзОтчетов.СоздатьЭлемент();
		НовЭлемент.Наименование = Выборка.Контрагент;
		НовЭлемент.Записать();
	КонецЦикла;

	//Заполнить сопоставления Новыми элементами
	
	РегистрыСведений.sprt_СопоставлениеКлиентов.ЗаполнитьОтсутствующимиПартнерамиИзФайлов();
	
	ДобавитьПлатежныеПорученияВБазу(ТЗРеестрОплат);

КонецПроцедуры



Процедура ДобавитьПлатежныеПорученияВБазу(ТЗРеестрОплат) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТЗВзаиморасчетов.Организация КАК Организация,
	|	ТЗВзаиморасчетов.Контрагент КАК Контрагент,
	|	ТЗВзаиморасчетов.ДатаНачала КАК ДатаНачала,
	|	ТЗВзаиморасчетов.ДатаОкончания КАК ДатаОкончания,
	|	ТЗВзаиморасчетов.Документ КАК Документ,
	|	ТЗВзаиморасчетов.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	ТЗВзаиморасчетов.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток,
	|	ТЗВзаиморасчетов.СуммаРасход КАК СуммаРасход,
	|	ТЗВзаиморасчетов.СуммаПриход КАК СуммаПриход,
	|	ТЗВзаиморасчетов.ИмяФайла КАК ИмяФайла
	|ПОМЕСТИТЬ ВТТЗВзаиморасчетов
	|ИЗ
	|	&ТЗВзаиморасчетов КАК ТЗВзаиморасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТЗВзаиморасчетов.Организация,
	|	ВТТЗВзаиморасчетов.Контрагент,
	|	ВТТЗВзаиморасчетов.Документ,
	|	ВТТЗВзаиморасчетов.СуммаРасход,
	|	ВТТЗВзаиморасчетов.СуммаПриход,
	|	ВТТЗВзаиморасчетов.ИмяФайла,
	|	sprt_НашиОрганизацииИзОтчетов.Организация КАК ОрганизацияИзБазы,
	|	sprt_ПартнерыИзОтчетов.Ссылка КАК ПартнерИзОтчета,
	|	sprt_НашиОрганизацииИзОтчетов.Ссылка КАК ОрганизацияИзОтчета
	|ПОМЕСТИТЬ ВТ_СоединенныеПлатежныеПоручения
	|ИЗ
	|	ВТТЗВзаиморасчетов КАК ВТТЗВзаиморасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.sprt_НашиОрганизацииИзОтчетов КАК sprt_НашиОрганизацииИзОтчетов
	|		ПО ВТТЗВзаиморасчетов.Организация = sprt_НашиОрганизацииИзОтчетов.Наименование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.sprt_ПартнерыИзОтчетов КАК sprt_ПартнерыИзОтчетов
	|		ПО ВТТЗВзаиморасчетов.Контрагент = sprt_ПартнерыИзОтчетов.Наименование
	|ГДЕ
	|	ВТТЗВзаиморасчетов.Документ ПОДОБНО ""%поручение исходящее%""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	sprt_СопоставлениеКлиентов.ПартнерИзБазы,
	|	sprt_СопоставлениеКлиентов.ПартнерИзОтчета
	|ПОМЕСТИТЬ ВТ_Сопоставления
	|ИЗ
	|	РегистрСведений.sprt_СопоставлениеКлиентов КАК sprt_СопоставлениеКлиентов
	|ГДЕ
	|	sprt_СопоставлениеКлиентов.Активно
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СоединенныеПлатежныеПоручения.Организация,
	|	ВТ_СоединенныеПлатежныеПоручения.Контрагент,
	|	ВТ_СоединенныеПлатежныеПоручения.Документ,
	|	ВТ_СоединенныеПлатежныеПоручения.СуммаРасход,
	|	ВТ_СоединенныеПлатежныеПоручения.СуммаПриход,
	|	ВТ_СоединенныеПлатежныеПоручения.ИмяФайла,
	|	ВТ_СоединенныеПлатежныеПоручения.ОрганизацияИзБазы,
	|	ВТ_СоединенныеПлатежныеПоручения.ПартнерИзОтчета,
	|	ВТ_СоединенныеПлатежныеПоручения.ОрганизацияИзОтчета,
	|	МАКСИМУМ(ВТ_Сопоставления.ПартнерИзБазы) КАК ПартнерИзБазы
	|ПОМЕСТИТЬ ВТ_ГотовыеПлатежныеПоручения
	|ИЗ
	|	ВТ_СоединенныеПлатежныеПоручения КАК ВТ_СоединенныеПлатежныеПоручения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Сопоставления КАК ВТ_Сопоставления
	|		ПО ВТ_СоединенныеПлатежныеПоручения.ПартнерИзОтчета = ВТ_Сопоставления.ПартнерИзОтчета
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СоединенныеПлатежныеПоручения.Организация,
	|	ВТ_СоединенныеПлатежныеПоручения.Контрагент,
	|	ВТ_СоединенныеПлатежныеПоручения.Документ,
	|	ВТ_СоединенныеПлатежныеПоручения.СуммаРасход,
	|	ВТ_СоединенныеПлатежныеПоручения.СуммаПриход,
	|	ВТ_СоединенныеПлатежныеПоручения.ИмяФайла,
	|	ВТ_СоединенныеПлатежныеПоручения.ОрганизацияИзБазы,
	|	ВТ_СоединенныеПлатежныеПоручения.ПартнерИзОтчета,
	|	ВТ_СоединенныеПлатежныеПоручения.ОрганизацияИзОтчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ГотовыеПлатежныеПоручения.Организация,
	|	ВТ_ГотовыеПлатежныеПоручения.Контрагент,
	|	ВТ_ГотовыеПлатежныеПоручения.Документ,
	|	ВТ_ГотовыеПлатежныеПоручения.СуммаРасход,
	|	ВТ_ГотовыеПлатежныеПоручения.СуммаПриход,
	|	ВТ_ГотовыеПлатежныеПоручения.ИмяФайла,
	|	ВТ_ГотовыеПлатежныеПоручения.ОрганизацияИзБазы,
	|	ВТ_ГотовыеПлатежныеПоручения.ПартнерИзОтчета,
	|	ВТ_ГотовыеПлатежныеПоручения.ОрганизацияИзОтчета,
	|	ВТ_ГотовыеПлатежныеПоручения.ПартнерИзБазы,
	|	sprt_ПлатежноеПоручение.Ссылка,
	|	sprt_ПлатежноеПоручение.ВерсияДанных,
	|	sprt_ПлатежноеПоручение.ПометкаУдаления,
	|	sprt_ПлатежноеПоручение.Номер,
	|	sprt_ПлатежноеПоручение.Дата,
	|	sprt_ПлатежноеПоручение.Проведен,
	|	sprt_ПлатежноеПоручение.ОрганизацияИзОтчета,
	|	sprt_ПлатежноеПоручение.ПартнерИзОтчета,
	|	sprt_ПлатежноеПоручение.ДатаСозданияВБазе,
	|	sprt_ПлатежноеПоручение.ДатаПоследнегоОбновления,
	|	sprt_ПлатежноеПоручение.НомерПлатежногоПоручения,
	|	sprt_ПлатежноеПоручение.ДатаПлатежногоПоручения,
	|	sprt_ПлатежноеПоручение.Организация,
	|	sprt_ПлатежноеПоручение.Партнер,
	|	sprt_ПлатежноеПоручение.НазваниеФайла,
	|	sprt_ПлатежноеПоручение.ПолныйПутьКФайлу,
	|	sprt_ПлатежноеПоручение.Сумма,
	|	sprt_ПлатежноеПоручение.ПоступлениеИзБазы,
	|	sprt_ПлатежноеПоручение.Наименование,
	|	sprt_ПлатежноеПоручение.Автор,
	|	sprt_ПлатежноеПоручение.Представление,
	|	sprt_ПлатежноеПоручение.МоментВремени
	|ИЗ
	|	ВТ_ГотовыеПлатежныеПоручения КАК ВТ_ГотовыеПлатежныеПоручения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.sprt_ПлатежноеПоручение КАК sprt_ПлатежноеПоручение
	|		ПО ВТ_ГотовыеПлатежныеПоручения.Документ = sprt_ПлатежноеПоручение.Наименование
	|ГДЕ
	|	sprt_ПлатежноеПоручение.Ссылка ЕСТЬ NULL";

	Запрос.УстановитьПараметр("ТЗВзаиморасчетов", ТЗРеестрОплат);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		//*-Платежное поручение исходящее 0001111020 от 22.11.2021 0:00:00
		
		НомерИДатаПП = СтрЗаменить(Выборка.Документ, "Платежное поручение исходящее ", "");
		НомерИДатаППМассивом = СтрРазделить(НомерИДатаПП,"от");
		Если НомерИДатаППМассивом.Количество() = 3 Тогда
		
			//*-Убрать префиксы
			НомерПоБанкуМассивом = СтрРазделить(СокрЛП(НомерИДатаППМассивом[0]),"-");
			НомерПоБанку = ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(НомерПоБанкуМассивом[?(НомерПоБанкуМассивом.Количество() = 1, 0, 1)]);
			ДатаПоБанкуСтрокой = Лев(СокрЛП(НомерИДатаППМассивом[2]),10);
			ДатаПоБанку = Дата(Прав(ДатаПоБанкуСтрокой, 4) + Сред(ДатаПоБанкуСтрокой, 4, 2) + Лев(ДатаПоБанкуСтрокой, 2));		
		Иначе
			НомерПоБанку = "";
			ДатаПоБанку = '00010101';
		КонецЕсли;
		
		ПутьКФайлуМассивом = СтрРазделить(Выборка.ИмяФайла,"\");
		НазваниеФайла = ?(ПутьКФайлуМассивом.Количество()>1, ПутьКФайлуМассивом[ПутьКФайлуМассивом.ВГраница()], "");
		
		
		
		НовЭлемент 								= Документы.sprt_ПлатежноеПоручение.СоздатьДокумент();

		НовЭлемент.Дата 						= ТекущаяДата();
		НовЭлемент.ОрганизацияИзОтчета 			= Выборка.ОрганизацияИзОтчета;
		НовЭлемент.ПартнерИзОтчета 				= Выборка.ПартнерИзОтчета;
		НовЭлемент.НомерПлатежногоПоручения 	= НомерПоБанку;
		НовЭлемент.ДатаПлатежногоПоручения 		= ДатаПоБанку;
		НовЭлемент.Организация 					= Выборка.ОрганизацияИзБазы;
		НовЭлемент.Партнер 						= Выборка.ПартнерИзБазы;
		НовЭлемент.НазваниеФайла  				= НазваниеФайла;
		НовЭлемент.ПолныйПутьКФайлу 			= Выборка.ИмяФайла;
		НовЭлемент.Сумма 						= Выборка.СуммаПриход;
		НовЭлемент.Наименование 				= Выборка.Документ;
		
		НовЭлемент.УстановитьНовыйНомер();
		НовЭлемент.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;
	
КонецПроцедуры
	